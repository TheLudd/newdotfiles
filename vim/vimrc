" For a paranoia.
" Normally `:set nocp` is not needed, because it is done automatically
" when .vimrc is found.
if &compatible
  " `:set nocp` has many side effects. Therefore this should be done
  " only when 'compatible' is set.
  set nocompatible
endif

" Open tests along with regular files
source ~/.vim/test-split.vim

" Basic config
syntax enable
set noswapfile
" Sane completion in command mode
set wildmenu
set wildmode=list:longest
" Detect file types for plugins/omni completion etc
filetype plugin indent on
" Highlight search matches while typing
set incsearch
" Use relative line numbers
set relativenumber
set number
" search with smartcase
set ignorecase
set smartcase

" Visualize preview of search/replace
set inccommand=split

" Leader commands, leader is '
let mapleader = "'"

" Edit vimrc
function! TabIsEmpty()
    return winnr('$') == 1 && len(expand('%')) == 0 && line2byte(line('$') + 1) <= 2
endfunction
function! EditFile(path)
    if TabIsEmpty()
        :execute "e " . a:path
    else
        :execute "tabe " . a:path
    endif
endfunction
nnoremap <leader>ev :call EditFile("$MYVIMRC")<cr>
" Source vimrc
nnoremap <leader>sv :source $MYVIMRC<cr>

" Plugins
" Use minpac as package manager
packadd minpac
call minpac#init()
nnoremap <leader>mu :source $MYVIMRC<cr>:call minpac#update()<cr>
nnoremap <leader>mc :source $MYVIMRC<cr>:call minpac#clean()<cr>

" minpac must have {'type': 'opt'} so that it can be loaded with `packadd`.
call minpac#add('k-takata/minpac', {'type': 'opt'})

" Use colorscheme jellybeans
call minpac#add('nanotech/jellybeans.vim')
colorscheme jellybeans

" Surround plugin
call minpac#add('tpope/vim-surround')

" Hardtime
call minpac#add('takac/vim-hardtime')
let g:hardtime_default_on = 0
let g:hardtime_allow_different_key = 1
let g:hardtime_maxcount = 2

" Multiple cursors
call minpac#add('terryma/vim-multiple-cursors')

" Javascript plugin
call minpac#add('pangloss/vim-javascript')

" Linter
call minpac#add('w0rp/ale')
let g:ale_fixers = {'javascript': ['eslint']}
let g:ale_linters = {'javascript': ['eslint']}
let g:ale_lint_delay = 500
" autofix
nnoremap <leader>l :ALEFix<cr>
" error navigation
noremap <leader>n :ALENextWrap<cr>
noremap <leader>N :ALEPreviousWrap<cr>

" Ctrl-P
call minpac#add('kien/ctrlp.vim')
let g:ctrlp_custom_ignore = '\v[\/](\.git|node_modules|dist)$'
let g:ctrlp_prompt_mappings = {
    \ 'AcceptSelection("e")': ['<c-t>'],
    \ 'AcceptSelection("t")': ['<cr>', '<2-LeftMouse>'],
    \ }

" JSX
call minpac#add('mxw/vim-jsx')

" exchange
call minpac#add('tommcdo/vim-exchange')

" exchange
call minpac#add('AndrewRadev/splitjoin.vim')

" rename
call minpac#add('danro/rename.vim')

" UltiSnips
call minpac#add('SirVer/ultisnips')
let g:UltiSnipsEditSplit = "vertical"
let g:UltiSnipsSnippetsDir = $HOME.'/.vim/UltiSnips/'
let g:UltiSnipsSnippetDirectories = [ $HOME.'/.vim/UltiSnips/' ]

" coffeescript
call minpac#add('kchmck/vim-coffee-script')

" cucumber
call minpac#add('tpope/vim-cucumber')

" emmet
call minpac#add('mattn/emmet-vim')

" ack - fast search
call minpac#add('mileszs/ack.vim')
if executable('ag')
  let g:ackprg = 'ag --vimgrep'
endif
cnoreabbrev Ack Ack
nnoremap <leader>w :Ack <C-r><C-w> . -r <CR>
nnoremap <leader>W :Ack<Space>

" Auto pairs
call minpac#add('jiangmiao/auto-pairs')

" Fugitive - git plugin
call minpac#add('tpope/vim-fugitive')
nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gr :Gdelete<cr>
nnoremap <leader>gch :Gread<cr>
nnoremap <leader>gca :Gcommit --all<cr>
nnoremap <leader>gco :Gcommit %<cr>
nnoremap <leader>gm :Gmove

" Tabularize
call minpac#add('godlygeek/tabular')
" align tabs each time | is entered
inoremap <silent> <Bar>   <Bar><Esc>:call <SID>align()<CR>a
function! s:align()
  let p = '^\s*|\s.*\s|\s*$'
  if exists(':Tabularize') && getline('.') =~# '^\s*|' && (getline(line('.')-1) =~# p || getline(line('.')+1) =~# p)
    let column = strlen(substitute(getline('.')[0:col('.')],'[^|]','','g'))
    let position = strlen(matchstr(getline('.')[0:col('.')],'.*|\s*\zs.*'))
    Tabularize/|/l1
    normal! 0
    call search(repeat('[^|]*|',column).'\s\{-\}'.repeat('.',position),'ce',line('.'))
  endif
endfunction

" generic snippets
call minpac#add('honza/vim-snippets')

" airline
call minpac#add('vim-airline/vim-airline')
let g:airline_section_y = ''
let g:airline_mode_map = {
    \ '__' : '-',
    \ 'n'  : 'N',
    \ 'i'  : 'I',
    \ 'R'  : 'R',
    \ 'c'  : 'C',
    \ 'v'  : 'V',
    \ 'V'  : 'V',
    \ '' : 'V',
    \ 's'  : 'S',
    \ }
let g:airline_section_z = ''

" Mappings

" Move easily between windows
noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

" Move lines up and down
nnoremap - :normal ddp<CR>
nnoremap _ @='kddpk'<CR>

" Move letters right/left
nnoremap <Right> xp
nnoremap <Left> hxph

" Use same command for upper/lower case
cnoreabbrev W w
cnoreabbrev Wa wa
cnoreabbrev Q q
cnoreabbrev Qa qa
cnoreabbrev X x
cnoreabbrev Xa xa

" Open/close files
cnoreabbrev tc tabclose
cnoreabbrev t tabedit

" search file with ctrl s
nnoremap <C-s> :%s/

" copy visual selection to clipboard
vnoremap <C-c> "+y

function! TabCloseRight(bang)
    let cur=tabpagenr()
    while cur < tabpagenr('$')
        exe 'tabclose' . a:bang . ' ' . (cur + 1)
    endwhile
endfunction

function! TabCloseLeft(bang)
    while tabpagenr() > 1
        exe 'tabclose' . a:bang . ' 1'
    endwhile
endfunction

command! -bang Tcr call TabCloseRight('<bang>')
command! -bang Tcl call TabCloseLeft('<bang>')

" Clear search with space
nnoremap <silent> <Space> :nohlsearch<Bar>:echo<CR>

" Remove trailing whitespace when saving
function! <SID>StripTrailingWhitespaces()
    if exists('b:noStripWhitespace')
        return
    endif

    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Do the business:
    %s/\s\+$//e
    " Clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction
autocmd BufWritePre *.snippets let b:noStripWhitespace=1
autocmd BufWritePre * :call <SID>StripTrailingWhitespaces()

" Indentation preferences
function! Indent2Spaces()
    setlocal expandtab
    setlocal shiftwidth=2
    setlocal softtabstop=2
endfunction
au BufRead,BufNewFile *.coffee,*.feature,*.js,*.jsx,*.json,*.html :call Indent2Spaces()

" Javascript leader commands
" Import chai assert
noremap <Leader>c ggOimport { assert } from 'chai'<ESC>0?\<import\><CR>o<CR>assert<ESC>Ic<TAB>
" Change test to use proxyquire
noremap <Leader>p 0wyeceproxyquiref'ci'proxy/describeoconst 0 = proxy()i''Pla, {}O
" Remove { return } in arrow functon
noremap <Leader>q [{r(/returndaw]}r)
" Curry function
noremap <Leader>h ^ceconstea =f(icurry(f)a =>f{]}a)?curry^
" Uncurry function
noremap <Leader>g ^/currydf($]}lx[{BdaWF=daw^cefunction
" turn function into arrow function
noremap <Leader>a ^ceconstea =f)a =>
" turn arrow function into function
noremap <Leader>f ^cefunctionf=daw;.
